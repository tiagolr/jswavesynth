desc:ADSR envelope

// Copyright (C) 2013-2017 Theo Niessink <theo@taletn.com>
// This work is free. You can redistribute it and/or modify it under the
// terms of the Do What The Fuck You Want To Public License, Version 2,
// as published by Sam Hocevar. See http://www.wtfpl.net/ for more details.

/* Example

   desc:Mono synth with ADSR

   slider1:3<0,5000,1>Attack (ms)
   slider2:1000<1,15000,1>Decay (ms)
   slider3:-12.0<-120.0,24.0,1.0>Sustain (dB)
   slider4:500<0,5000,1>Release (ms)

   import Tale/adsr.jsfx-inc
   import Tale/midi_queue.jsfx-inc
   import Tale/poly_blep.jsfx-inc

   @slider

   adsr.adsr_seta(slider1 * 0.001);
   adsr.adsr_setd(slider2 * 0.001);
   adsr.adsr_sets(exp(log(10)/20 * slider3));
   adsr.adsr_setr(slider4 * 0.001);

   @sample

   while(midi.midiq_recv()) (
     midi.msg1 &= 0xF0;

     // Note On
     midi.msg1 == 0x90 && midi.msg3 ? (
       osc.poly_setf(note = midi.msg2, 440);
       // Attack
       adsr.adsr_a(0.5 * midi.msg3 / 128);
     ) :

     // Note Off
     midi.msg1 == 0x80 || midi.msg1 == 0x90 ? (
       // Release
       midi.msg2 == note ? adsr.adsr_r();
     );
   );

   // Process ADSR envelope and apply it to oscillator.
   adsr.adsr_process() ? spl0 = spl1 = adsr.env * osc.poly_saw();

   Setting Functions

    * adsr_seta(time) -- Attack
    * adsr_setd(time) -- Decay
    * adsr_sets(gain) -- Sustain
    * adsr_setr(time) -- Release
      Example: adsr.adsr_seta(0.003);
      Sets the ADSR envelope time (specified in seconds) or the gain
      [0.0..1.0] factor, and returns the coefficient or the gain factor.

   Processing Functions

    * adsr_a(scale) -- Attack
      Example: adsr.adsr_a(1.0);
      Starts processing the ADSR envelope, scaling it to [0.0..scale], and
      returns the state.

    * adsr_d()      -- Decay
    * adsr_s()      -- Sustain
    * adsr_r()      -- Release
      Example: adsr.adsr_r();
      Continues processing the ADSR envelope, and returns the state.

    * adsr_process()
      Example: state = adsr.adsr_process();
      Processes the ADSR envelope, and returns its state.

      Note: You should call this function from the @sample section.

    * adsr_reset()
      Example: adsr.adsr_reset();
      Resets the ADSR envelope, and returns the state.

   Instance Variables

    * state
      Example: released = !adsr.state;
      The current state (1=attack, 2=decay, 4=sustain, 8=release, 0=off).

    * env
      Example: spl0 *= adsr.env;
      The scaled ADSR envelope output.

    * scale
      Example: adsr2.adsr_a(adsr1.scale);
      The current scale factor.

*/

// buf[0] = env
// buf[1] = a
// buf[2] = d
// buf[3] = s
// buf[4] = r
// buf[5] = state
// buf[6] = scale

@init

function _adsr_set(time)
(
  1 / (0.2 * time * srate + 1);
);

function adsr_seta(time, buf)
  //instance(a)
(
  buf[1] = _adsr_set(time);
);

function adsr_setd(time, buf)
  //instance(d)
(
  buf[2] = _adsr_set(time);
);

function adsr_sets(gain, buf)
  //instance(s)
(
  buf[3] = gain;
);

function adsr_setr(time, buf)
  //instance(r)
(
  buf[4] = _adsr_set(time);
);

function adsr_reset(buf)
  //instance(state, env)
(
  buf[0] = 0;
  buf[5] = 0;
);

function adsr_r(buf)
  // instance(state, r)
(
  buf[4] < 1 ? ( // r < 1 ? state = 8 : adsr_reset()
    buf[5] = 8;
  ) : (
    adsr_reset(buf);
  );
);

function adsr_s(buf)
  // instance(state, s, env, scale)
(
  buf[0] = buf[6] * buf[3]; // env = scale * s
  buf[5] = 4; // state = 4
);

function adsr_d(buf)
//  instance(state, d)
(
  buf[2] < 1 ? ( // d < 1
    buf[5] = 2; // state = 2
  ) : (
    adsr_s(buf);
  );
);

function adsr_a(scale, buf)
  // instance(state, a, env)
(
  buf[6] = scale; // this.scale = scale

  buf[1] < 1 ? ( // a < 1
    buf[5] = 1; // state = 1
  ) : (
    buf[0] = scale; // env = scale
    adsr_d(buf);
  );
);

function adsr_process(buf)
  //instance(state, a, d, s, r, env, scale)
(
  env = buf[0];
  a = buf[1];
  d = buf[2];
  s = buf[3];
  r = buf[4];
  state = buf[5];
  scale = buf[6];
  buf[5] ? (

    // Decay
    buf[5] == 2 ? (
      buf[0] += d * (scale * s - env);
      abs(buf[0] - scale * s) <= scale * 0.000001 ? adsr_s(buf);
    ) :

    // Release
    buf[5] == 8 ? (
      buf[0] += r * (0 - env);
      abs(buf[0]) <= scale * 0.000001 ? adsr_reset(buf);
    ) :

    // Attack
    buf[5] == 1 ? (
      buf[0] += a * (scale - env);
      abs(buf[0] - scale) <= scale * exp(-5) ? adsr_d(buf);
    );
  );

  buf[5];
);
